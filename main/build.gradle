apply plugin: 'com.android.application'

android {
    compileSdk 34

    defaultConfig {
        applicationId "im.zego.express.example.video"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode getAppVersionCode()
        versionName getAppVersionName()

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }

        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file("../../../../../buildscripts/config/android/release_key.jks")
            storePassword "your_keystore_password"
            keyAlias "your_key_alias"
            keyPassword "your_key_password"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            if (project.hasProperty('enable-hwasan')) {
                // Set this property by "gradlew -Penable-hwasan=true"
                if (project.property('enable-hwasan').toBoolean()) {
                    // Add "wrap.sh" to the apk for HWASAN
                    resources.srcDir 'hwasan'
                    jniLibs.srcDir 'asanlibs'
                }
            } else if (project.hasProperty('enable-asan')) {
                // Set this property by "gradlew -Penable-asan=true"
                if (project.property('enable-asan').toBoolean()) {
                    // Add "wrap.sh" to the apk for ASAN
                    resources.srcDir 'asan'
                    jniLibs.srcDir 'asanlibs'
                }
            }
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.30'
//    implementation 'im.zego:express-video:3.21.0'
    implementation 'com.tencent.bugly:crashreport:latest.release'
    implementation project(path: ':AdvancedAudioProcessing')
    implementation project(path: ':AdvancedVideoProcessing')
    implementation project(path: ':CommonFeatures')
    implementation project(path: ':DebugAndConfig')
    implementation project(path: ':Others')
    implementation project(path: ':Quickstart')
    implementation project(path: ':AdvancedStreaming')
    implementation project(path: ':Scenes')
    implementation(name: 'ZegoExpressEngine', ext: 'aar')
    //todo --- fbEffect start
    implementation project(":fbui")
    implementation project(':CommonTools')
    //todo --- fbEffect end
}

def getAppVersionCode() {
    int versionCode = 1
    if (System.getenv("BUILD_NUMBER") != null) {
        versionCode = Integer.parseInt(System.getenv("BUILD_NUMBER"))
    }
    return versionCode
}

def getAppVersionName() {
    String versionName = '1.0.0'
    File packageJson = file('../../../../../package.json')
    if (packageJson.exists() && packageJson.getName() == 'package.json') {
        versionName = new groovy.json.JsonSlurper().parse(packageJson).version
    }
    versionName += '.' + getAppVersionCode().toString()
    return versionName
}
